;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; MODULE      : version-update.scm
;; DESCRIPTION : 版本更新检查（开发者配置）
;; COPYRIGHT   : (C) 2026  Mogan STEM authors
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(texmacs-module (utils misc version-update))

;; ============================================
;; 开发者配置区（修改此处调整行为）
;; ============================================

(define SNOOZE-DAYS 3)

;; Mock 远程版本号（用于测试，设为 #f 则使用真实网络请求）
;; 示例：(define MOCK-REMOTE-VERSION "2026.3.0")

(define MOCK-REMOTE-VERSION #f)

;; ============================================
;; 内部实现
;; ============================================

;; 获取 Mock 远程版本号（优先从持久化存储读取）
(tm-define (get-mock-remote-version)
  (:secure #t)
  (let ((stored (persistent-get (get-texmacs-home-path) MOCK-VERSION-KEY)))
    (if (and stored (!= stored "")) stored MOCK-REMOTE-VERSION)
  ) ;let
) ;tm-define

;; 设置 Mock 远程版本号（同时保存到持久化存储）
(tm-define (set-mock-remote-version! version)
  (:secure #t)
  (set! MOCK-REMOTE-VERSION version)
  (persistent-set (get-texmacs-home-path) MOCK-VERSION-KEY version)
) ;tm-define

;; 清除 Mock 远程版本号（恢复默认 #f）
(tm-define (clear-mock-remote-version)
  (:secure #t)
  (set! MOCK-REMOTE-VERSION #f)
  (persistent-remove (get-texmacs-home-path) MOCK-VERSION-KEY)
) ;tm-define

(define LAST-CHECK-KEY "version_last_check")

(define SNOOZE-UNTIL-KEY "version_snooze_until")

(define MOCK-VERSION-KEY "version_mock_remote")

(define IGNORED-VERSION-KEY "version_ignored_remote")

(define (current-timestamp)
  (current-time)
) ;define

;; 检查是否应该检查更新（考虑稍后提醒时间）
(tm-define (should-check-version-update?)
  (:secure #t)
  (let* ((now (current-timestamp))
         (snooze-until (or (persistent-get (get-texmacs-home-path) SNOOZE-UNTIL-KEY) "0")
         ) ;snooze-until
         (snooze-time (if (== snooze-until "") 0 (string->number snooze-until)))
        ) ;
    (>= now snooze-time)
  ) ;let*
) ;tm-define

(tm-define (version-update-snooze-until)
  (:secure #t)
  (or (persistent-get (get-texmacs-home-path) SNOOZE-UNTIL-KEY) "0")
) ;tm-define

(tm-define (clear-version-update-snooze-history)
  (:secure #t)
  (persistent-remove (get-texmacs-home-path) SNOOZE-UNTIL-KEY)
) ;tm-define

;; 强制清除所有记录（用于测试）
(tm-define (clear-version-update-history)
  (:secure #t)
  (clear-version-update-snooze-history)
  (persistent-remove (get-texmacs-home-path) IGNORED-VERSION-KEY)
  (clear-mock-remote-version)
) ;tm-define

;; 稍后提醒（使用默认间隔）
(tm-define (snooze-version-update)
  (:secure #t)
  (let* ((now (current-timestamp)) (future (+ now (* SNOOZE-DAYS 24 3600))))
    (persistent-set (get-texmacs-home-path)
      SNOOZE-UNTIL-KEY
      (number->string future)
    ) ;persistent-set
  ) ;let*
) ;tm-define

(tm-define (get-ignored-version-update)
  (:secure #t)
  (or (persistent-get (get-texmacs-home-path) IGNORED-VERSION-KEY) "")
) ;tm-define

(tm-define (version-update-ignored? remote-version)
  (:secure #t)
  (and (string? remote-version)
    (!= remote-version "")
    (== remote-version (get-ignored-version-update))
  ) ;and
) ;tm-define

(tm-define (ignore-version-update remote-version)
  (:secure #t)
  (if (or (not (string? remote-version)) (== remote-version ""))
    (persistent-remove (get-texmacs-home-path) IGNORED-VERSION-KEY)
    (persistent-set (get-texmacs-home-path) IGNORED-VERSION-KEY remote-version)
  ) ;if
) ;tm-define

;; 获取下载页URL
;; 社区版跳转到 mogan.app，商业版跳转到 liiistem.cn/com
(tm-define (get-update-download-url)
  (:secure #t)
  (if (community-stem?)
    ;; 社区版官网
    (if (== (get-output-language) "chinese")
      "https://mogan.app/zh/"
      "https://mogan.app/en/"
    ) ;if
    ;; 商业版官网
    (if (== (get-output-language) "chinese")
      "https://liiistem.cn/install.html"
      "https://liiistem.com/install.html"
    ) ;if
  ) ;if
) ;tm-define
